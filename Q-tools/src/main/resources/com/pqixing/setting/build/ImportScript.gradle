//version=1.1,date=2020.06.10
try {
    Script.runScript(rootDir, gradle)
} catch (Exception e) {
    new File(rootDir, "build/ImportScript.gradle").delete(); throw new RuntimeException(e)
}
/**
 * 导入脚本
 */
class Script {
    static final  String defaultXmlPath = "templet/project.xml"

    public static void runScript(File rootDir, Gradle gradle) {
        File defaultXml = new File(rootDir, defaultXmlPath)
        if (!defaultXml.exists()) return
        def c = new InCludes(rootDir, gradle)
        HashSet<String> includes = c.includes
        def codeRoot = c.codeRoot

        //解析default xml 加载所有的git信息
        Node gitNode = new XmlParser().parse(defaultXml)
        def paths = new HashMap<String, String>()
        loadProjectPath(includes,paths, gitNode, codeRoot)

//        println("Parse paths = $paths")

        gradle.settingsEvaluated { org.gradle.api.initialization.Settings setting ->

            includes.each {i->
                def result = paths.get(i)
                if(result !=null){
                    setting.include(":$i")
                    setting.findProject(":$i").projectDir = new File(result)
                }
            }
        }
    }

    static void loadProjectPath(HashSet<String> includes, HashMap<String, String> paths, Node node, String path) {
        def groups = node.group
        if (groups.size() > 0) groups.each { Node n ->
            loadProjectPath(includes, paths, n, path + "/${n.@name}")
        }

        def projects = node.project
//        println("loadProjectPath -------> ${projects}")
        if (projects.size() > 0) projects.each { Node p ->
            loadModulePath(includes, paths, p, path + "/${p.@name}")
            if (p.@type != null) addModulePath(includes, paths, p, path)
        }
    }

    static  void loadModulePath(HashSet<String> includes, HashMap<String, String> paths, Node node, String path) {
        def groups = node.group
        if (groups.size() > 0) groups.each { Node n ->
            loadModulePath(includes, paths, n, path + "/${n.@name}")
        }

        def submodules = node.submodule
        if (submodules.size() > 0) submodules.each { Node s ->
            addModulePath(includes, paths, s, path)
        }
    }

    static void addModulePath(HashSet<String> includes, HashMap<String, String> paths, Node node, String path) {
        def name = node.@name+""
        paths.put(name,"$path/$name")
        def api = node.@api
        if(api!=null){
            def apiStr = api.toString()
            includes.add("this"== apiStr?"${name}_api":apiStr)
        }
    }

}

/**
 * include 配置读取
 */
class InCludes {
    public HashSet<String> includes;
    public String codeRoot;
    private Class clazz = null

    public InCludes(File rootDir, Gradle gradle) {
        String value
        def list = Arrays.asList("include", "codeRoot")
        for (String key : list) {
            try {
                value = System.getProperty(key)
            } catch (Exception e) {
            }
            if (value == null) value = getValue(key, rootDir)
            switch (key) {
                case "include":
                    includes = parseInclude(value.trim(), rootDir, gradle)
                    break
                case "codeRoot":
                    codeRoot = value
                    break
                default: gradle.println("not found  -> $value")
            }
        }
        println("Parse include = $includes codeRoot = $codeRoot")
    }

    private HashSet<String> parseInclude(String include, File rootDir, Gradle gradle) {
        def list = new HashSet<String>()
        if (include.isEmpty() || include == "Auto") {//自动抓取工程导入
            for (task in gradle.startParameter.taskNames) {
                def tks = task.split(":")
                if (tks.length < 2) continue
                list.add(tks[tks.length - 2])
            }
            return list
        }
        def values = new LinkedList<String>()
        include.replace("+", ",").split(",").each {
            values.add(it.trim())
        }
        Collections.sort(values, new Comparator<String>() {
            @Override
            int compare(String t, String t1) {
                return (t.contains("#") ? t.substring(0, 1) : "A") <=> (t1.contains("#") ? t1.substring(0, 1) : "A")
            }
        })
        values.each { v ->
            String l = v.trim().replaceAll(".*#", "")
            if (!v.contains("#")) list.add(l)
            else if (v.startsWith("E#")) list.remove(l)
            else if (v.startsWith("D#")) handleDps(list, rootDir, l, true)
            else if (v.startsWith("ED#")) handleDps(list, rootDir, l, false)
        }
        return list
    }

    private void handleDps(HashSet<String> source, File rootDir, String module, boolean add) {
        File d = new File(rootDir, "build/dps/${module}.dp")
        if (add) source.add(module.trim()) else source.remove(module.trim())
        if (!d.exists()) module.println("File no exists -> $d.absolutePath , please excute task DpsAnalysis befor import!!!")
        else {
            d.text.split(",").each { if (add) source.add(it.trim()) else source.remove(it.trim()) }
        }
    }

    private String getValue(String key, File rootDir) {
        try {
            if (clazz == null) clazz = new GroovyClassLoader().parseClass(new File(rootDir, "Config.java"))

            def field = clazz.getField(key)
            field.setAccessible(true)
            return field.get(clazz.newInstance()).toString()
        } catch (Exception e) {
            key.println("Exception -> getValue key = $key ,$e ${clazz.fields.size()}")
            return ""
        }
    }
}
